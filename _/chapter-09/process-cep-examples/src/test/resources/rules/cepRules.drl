package com.wordrpess.marianbuenosayres;

import com.wordpress.marianbuenosayres.model.Requirement;
import com.wordpress.marianbuenosayres.service.NotifierService;

import org.kie.api.runtime.process.WorkflowProcessInstance;

global NotifierService notifier;

declare ProcessEvent
    @role(event)
    processId: String
    state: Integer
    reqVariable: Object
end

rule "create events for each process instance"
when
    pi: WorkflowProcessInstance()
then
    ProcessEvent event = new ProcessEvent(pi.getProcessId(), pi.getState(), pi.getVariable("req"));
    insert(event);
end 

rule "two processes at least 3 minutes appart"
when
    o: Object() from entry-point "tempop-test"
    p1: ProcessEvent($processId1 : processId)
    p2: ProcessEvent(this after[3m] p1, processId == $processId1)
then
    System.out.println(drools.getRule().getName());
end

rule "reqPriorityProcess: more than 10 processes in 5 minutes"
when
    Number(intValue > 10) from accumulate(
        pi: ProcessEvent(
            processId == "reqPriorityProcess", 
            state == WorkflowProcessInstance.STATE_ACTIVE,
            reqVariable != null) 
        over window:time(5m), 
        count(pi)
    )
then
    notifier.sendWarning(drools.getRule().getName());
end


